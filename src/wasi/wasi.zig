const std = @import("std");
const builtin = @import("builtin");
const mem = std.mem;
const fs = std.fs;
const posix = std.posix;
const math = std.math;
const wasi = std.os.wasi;
const native_os = builtin.os.tag;

const VirtualMachine = @import("../instance/vm.zig").VirtualMachine;
const WasmError = @import("../instance/vm.zig").WasmError;

// I've largely used the implementations from https://github.com/andrewrk/zig-wasi (shoutout to Andrew and Jacob!).
//
// Thought: could we move the wasi implementations into the zig standard library, or are they too specific to a particular
// WebAssembly implementation? E.g. some of the wasi functions only seem to depend on a slice of memory + the zig std lib
// functions.

pub fn args_get(vm: *VirtualMachine) WasmError!void {
    const argv_buf_ptr = vm.popOperand(u32);
    const argv_ptr = vm.popOperand(u32);

    const memory = try vm.inst.getMemory(0);
    const data = memory.memory();

    var argv_buf_i: usize = 0;
    for (vm.wasi_args.items, 0..) |arg, i| {
        const argv_i_ptr = argv_buf_ptr + argv_buf_i;
        const arg_len = arg.len + 1;

        @memcpy(data[argv_i_ptr .. argv_i_ptr + arg_len], arg[0..arg_len]);
        argv_buf_i += arg_len;

        try memory.write(u32, argv_ptr, 4 * @as(u32, @intCast(i)), @as(u32, @intCast(argv_i_ptr)));
    }

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn args_sizes_get(vm: *VirtualMachine) WasmError!void {
    const argv_buf_size_ptr = vm.popOperand(u32);
    const argc_ptr = vm.popOperand(u32);

    const memory = try vm.inst.getMemory(0);

    const argc = vm.wasi_args.items.len;
    try memory.write(u32, argc_ptr, 0, @as(u32, @intCast(argc)));

    var buf_size: usize = 0;
    for (vm.wasi_args.items) |arg| {
        buf_size += arg.len + 1;
    }
    try memory.write(u32, argv_buf_size_ptr, 0, @as(u32, @intCast(buf_size)));

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn environ_get(vm: *VirtualMachine) WasmError!void {
    const environ_buf_ptr = vm.popOperand(u32);
    const environ_ptr = vm.popOperand(u32);

    const memory = try vm.inst.getMemory(0);
    const data = memory.memory();

    var environ_buf_i: usize = 0;
    var i: usize = 0;
    var iter = vm.wasi_env.iterator();
    while (iter.next()) |entry| {
        const env_i_ptr = environ_buf_ptr + environ_buf_i;
        const key = entry.key_ptr.*;
        const value = entry.value_ptr.*;

        // Write "KEY=value\0" to WASI memory
        @memcpy(data[env_i_ptr..][0..key.len], key);
        data[env_i_ptr + key.len] = '=';
        @memcpy(data[env_i_ptr + key.len + 1 ..][0..value.len], value);
        data[env_i_ptr + key.len + 1 + value.len] = 0;

        const env_len = key.len + 1 + value.len + 1; // key + '=' + value + '\0'
        try memory.write(u32, environ_ptr, 4 * @as(u32, @intCast(i)), @as(u32, @intCast(env_i_ptr)));

        environ_buf_i += env_len;
        i += 1;
    }

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn environ_sizes_get(vm: *VirtualMachine) WasmError!void {
    const environ_buf_size_ptr = vm.popOperand(u32);
    const environc_ptr = vm.popOperand(u32);

    const memory = try vm.inst.getMemory(0);

    const environc = vm.wasi_env.count();
    try memory.write(u32, environc_ptr, 0, @as(u32, @intCast(environc)));

    var buf_size: usize = 0;
    var iter = vm.wasi_env.iterator();
    while (iter.next()) |entry| {
        // key + '=' + value + '\0'
        buf_size += entry.key_ptr.*.len + 1 + entry.value_ptr.*.len + 1;
    }
    try memory.write(u32, environ_buf_size_ptr, 0, @as(u32, @intCast(buf_size)));

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn clock_time_get(vm: *VirtualMachine) WasmError!void {
    const timestamp_ptr = vm.popOperand(u32);
    const precision = vm.popOperand(i64); // FIXME: we should probably be using this
    _ = precision;
    const clock_id = vm.popOperand(i32); // FIXME: we should probably be using this
    _ = clock_id;

    const memory = try vm.inst.getMemory(0);

    const timestamp = toWasiTimestamp(std.time.nanoTimestamp());

    try memory.write(u64, timestamp_ptr, 0, timestamp);

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn fd_close(vm: *VirtualMachine) WasmError!void {
    const fd = vm.popOperand(i32);

    const host_fd = vm.getHostFd(fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }
    posix.close(host_fd);

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn fd_fdstat_get(vm: *VirtualMachine) WasmError!void {
    const stat_ptr = vm.popOperand(u32);
    const fd = vm.popOperand(i32);

    const memory = try vm.inst.getMemory(0);

    const host_fd = vm.getHostFd(fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }
    const file = fs.File{ .handle = host_fd };
    const stat = file.stat() catch |err| {
        try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
        return;
    };

    // Write fdstat structure:
    // offset 0x00: u8 fs_filetype
    // offset 0x02: u16 fs_flags
    // offset 0x08: u64 fs_rights_base
    // offset 0x10: u64 fs_rights_inheriting
    try memory.write(u8, stat_ptr, 0x00, @intFromEnum(toWasiFileType(stat.kind)));
    try memory.write(u16, stat_ptr, 0x02, 0); // fs_flags

    // Grant all rights for now (FIXME: should be more restrictive)
    try memory.write(u64, stat_ptr, 0x08, math.maxInt(u64));
    try memory.write(u64, stat_ptr, 0x10, math.maxInt(u64));

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

// FIXME: implement
pub fn fd_fdstat_set_flags(vm: *VirtualMachine) WasmError!void {
    const param0 = vm.popOperand(i32);
    const param1 = vm.popOperand(i32);
    std.debug.print("Unimplemented: fd_fdstat_set_flags({}, {})\n", .{ param0, param1 });
    try vm.pushOperand(u64, 0);
    @panic("Unimplemented: fd_fdstat_set_flags");
}

pub fn fd_filestat_get(vm: *VirtualMachine) WasmError!void {
    const stat_ptr = vm.popOperand(u32);
    const fd = vm.popOperand(i32);

    const memory = try vm.inst.getMemory(0);

    const host_fd = vm.getHostFd(fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }
    const file = std.fs.File{ .handle = host_fd };
    const stat = file.stat() catch |err|
        {
            try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
            return;
        };

    try memory.write(u64, stat_ptr, 0, 0); // device id
    try memory.write(u64, stat_ptr, 8, stat.inode); // inode
    try memory.write(u64, stat_ptr, 16, @intFromEnum(toWasiFileType(stat.kind))); // filetype
    try memory.write(u64, stat_ptr, 24, 1); // nlink - hard links refering to this file count
    try memory.write(u64, stat_ptr, 32, stat.size); // size in bytes
    try memory.write(u64, stat_ptr, 40, @as(u64, @intCast(stat.atime))); // atime - last access time
    try memory.write(u64, stat_ptr, 48, @as(u64, @intCast(stat.mtime))); // mtime - last modified time
    try memory.write(u64, stat_ptr, 56, @as(u64, @intCast(stat.ctime))); // ctime - last status change time

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn fd_prestat_get(vm: *VirtualMachine) WasmError!void {
    const prestat_ptr = vm.popOperand(u32);
    const fd = vm.popOperand(i32);

    const memory = try vm.inst.getMemory(0);

    if (vm.lookupWasiPreopen(fd)) |preopen| {
        // Write prestat structure:
        // offset 0: u8 tag (0 = PREOPENTYPE_DIR)
        // offset 4: u32 pr_name_len
        try memory.write(u8, prestat_ptr, 0, 0); // tag = 0 (PREOPENTYPE_DIR)
        try memory.write(u32, prestat_ptr, 4, @as(u32, @intCast(preopen.name.len)));

        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
    } else {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
    }
}

pub fn fd_prestat_dir_name(vm: *VirtualMachine) WasmError!void {
    const path_len = vm.popOperand(u32); // FIXME: we should probably be using this
    _ = path_len;
    const path_ptr = vm.popOperand(u32);
    const fd = vm.popOperand(i32);

    const memory = try vm.inst.getMemory(0);

    const preopen = vm.lookupWasiPreopen(fd) orelse return WasmError.Trap;
    try memory.copy(path_ptr, preopen.name);

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn fd_read(vm: *VirtualMachine) WasmError!void {
    const n_read_ptr = vm.popOperand(u32);
    const iovs_len = vm.popOperand(u32);
    const iovs_ptr = vm.popOperand(u32);
    const fd = vm.popOperand(i32);

    const memory = try vm.inst.getMemory(0);
    const data = memory.memory();

    const host_fd = vm.getHostFd(fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }

    var i: u32 = 0;
    var total_read: usize = 0;
    while (i < iovs_len) : (i += 1) {
        const offset: u32 = i * 8; // Each iov is 8 bytes...
        const iov_i_ptr = try memory.read(u32, iovs_ptr, offset); // 4 bytes (u32) for the ith ptr of where to read into
        const iov_i_len = try memory.read(u32, iovs_ptr, offset + 4); // 4 bytes (u32) for the length of data to read

        const buf = data[iov_i_ptr .. iov_i_ptr + iov_i_len];

        // read data from fd into buffer defined by iov
        const read = posix.read(host_fd, buf) catch |err| {
            try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
            return;
        };

        total_read += read;
        if (read != buf.len) break;
    }

    try memory.write(u32, n_read_ptr, 0, @as(u32, @intCast(total_read)));

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn fd_seek(vm: *VirtualMachine) WasmError!void {
    const new_offset_ptr = vm.popOperand(u32);
    const relative_to: wasi.whence_t = @enumFromInt(vm.popOperand(i32));
    const offset = vm.popOperand(i64);
    const fd = vm.popOperand(i32);

    const host_fd = vm.getHostFd(fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }

    switch (relative_to) {
        wasi.whence_t.CUR => {
            posix.lseek_CUR(host_fd, offset) catch |err| {
                try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
                return;
            };
        },
        wasi.whence_t.END => {
            posix.lseek_END(host_fd, offset) catch |err| {
                try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
                return;
            };
        },
        wasi.whence_t.SET => {
            posix.lseek_SET(host_fd, @intCast(offset)) catch |err| {
                try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
                return;
            };
        },
    }

    const new_offset = posix.lseek_CUR_get(host_fd) catch |err| {
        try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
        return;
    };

    const memory = try vm.inst.getMemory(0);
    try memory.write(u64, new_offset_ptr, 0, new_offset);

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn fd_write(vm: *VirtualMachine) WasmError!void {
    const ret_ptr = vm.popOperand(u32);
    const iovs_len = vm.popOperand(u32);
    const iovs_ptr = vm.popOperand(u32);
    const fd = vm.popOperand(i32);

    const memory = try vm.inst.getMemory(0);
    const data = memory.memory();

    const host_fd = vm.getHostFd(fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }

    var n: usize = 0;
    var i: u32 = 0;
    while (i < iovs_len) : (i += 1) {
        const offset: u32 = i * 8;
        const iov_i_ptr = try memory.read(u32, iovs_ptr, offset);
        const iov_i_len = try memory.read(u32, iovs_ptr, offset + 4);

        const bytes = data[iov_i_ptr .. iov_i_ptr + iov_i_len];

        const written = posix.write(host_fd, bytes) catch |err| {
            try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
            return;
        };

        n += written;

        if (written != bytes.len) break;
    }

    try memory.write(u32, ret_ptr, 0, @as(u32, @intCast(n)));

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

// FIXME: implement
pub fn path_create_directory(vm: *VirtualMachine) WasmError!void {
    const param0 = vm.popOperand(i32);
    const param1 = vm.popOperand(i32);
    const param2 = vm.popOperand(i32);
    std.debug.print("Unimplemented: path_create_directory({}, {}, {})\n", .{ param0, param1, param2 });
    try vm.pushOperand(u64, 0);
    @panic("Unimplemented: path_create_directory");
}

pub fn path_filestat_get(vm: *VirtualMachine) WasmError!void {
    const stat_ptr = vm.popOperand(u32);
    const path_len = vm.popOperand(u32);
    const path_ptr = vm.popOperand(u32);
    const flags = vm.popOperand(u32); // FIXME: we should probably be using this
    _ = flags;
    const fd = vm.popOperand(i32);

    const memory = try vm.inst.getMemory(0);
    const data = memory.memory();

    const sub_path = data[path_ptr .. path_ptr + path_len];

    // Validate path for security: reject absolute paths and parent directory references
    if (sub_path.len > 0 and sub_path[0] == '/') {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.NOTCAPABLE));
        return;
    }
    if (mem.indexOf(u8, sub_path, "..") != null) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.NOTCAPABLE));
        return;
    }

    const host_fd = vm.getHostFd(fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }
    const dir: fs.Dir = .{ .fd = host_fd };
    const stat = dir.statFile(sub_path) catch |err| {
        try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
        return;
    };

    try memory.write(u64, stat_ptr, 0, 0);
    try memory.write(u64, stat_ptr, 0x08, stat.inode);
    try memory.write(u64, stat_ptr, 0x10, @intFromEnum(toWasiFileType(stat.kind)));
    try memory.write(u64, stat_ptr, 0x18, 1);
    try memory.write(u64, stat_ptr, 0x20, stat.size);
    try memory.write(u64, stat_ptr, 0x28, toWasiTimestamp(stat.atime));
    try memory.write(u64, stat_ptr, 0x30, toWasiTimestamp(stat.mtime));
    try memory.write(u64, stat_ptr, 0x38, toWasiTimestamp(stat.ctime));

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

pub fn path_open(vm: *VirtualMachine) WasmError!void {
    const fd_ptr = vm.popOperand(u32);

    const fs_flags: wasi.fdflags_t = @bitCast(@as(u16, @truncate(vm.popOperand(u32))));

    const fs_rights_inheriting = vm.popOperand(u64); // FIXME: we should probably be using this
    _ = fs_rights_inheriting;

    const fs_rights_base: wasi.rights_t = @bitCast(vm.popOperand(u64));

    const oflags: wasi.oflags_t = @bitCast(@as(u16, @truncate(vm.popOperand(u32))));

    const path_len = vm.popOperand(u32);
    const path_ptr = vm.popOperand(u32);
    const dir_flags = vm.popOperand(u32); // FIXME: we should probably be using this
    _ = dir_flags;
    const dir_fd = vm.popOperand(i32);

    const memory = try vm.inst.getMemory(0);
    const data = memory.memory();

    const sub_path = data[path_ptr .. path_ptr + path_len];

    const host_fd = vm.getHostFd(dir_fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }

    const flags = posix.O{
        .CREAT = oflags.CREAT,
        .DIRECTORY = oflags.DIRECTORY,
        .EXCL = oflags.EXCL,
        .TRUNC = oflags.TRUNC,

        .APPEND = fs_flags.APPEND,
        .DSYNC = fs_flags.DSYNC,
        .NONBLOCK = fs_flags.NONBLOCK,
        .SYNC = fs_flags.SYNC,

        .ACCMODE = if (fs_rights_base.FD_READ and fs_rights_base.FD_WRITE) blk: {
            break :blk .RDWR;
        } else if (fs_rights_base.FD_WRITE) blk: {
            break :blk .WRONLY;
        } else blk: {
            // Default to RDONLY if no rights or only FD_READ
            break :blk .RDONLY;
        },
    };

    const mode = 0o644;

    // Try to open with the requested flags first
    const opened_fd = posix.openat(host_fd, sub_path, flags, mode) catch |err| blk: {
        // If ISDIR error and we didn't request O_DIRECTORY, try again with O_DIRECTORY and RDONLY
        if (err == error.IsDir and !oflags.DIRECTORY) {
            var retry_flags = flags;
            retry_flags.DIRECTORY = true;
            retry_flags.ACCMODE = .RDONLY;
            break :blk posix.openat(host_fd, sub_path, retry_flags, mode) catch |retry_err| {
                try vm.pushOperand(u64, @intFromEnum(toWasiError(retry_err)));
                return;
            };
        } else {
            try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
            return;
        }
    };

    // Add the opened file to the wasi preopens map
    // Use the host_fd as both the wasi_fd and host_fd (direct mapping)
    // FIXME: This should use a separate fd allocation strategy
    vm.inst.addWasiPreopen(opened_fd, "", opened_fd) catch {
        _ = posix.close(opened_fd);
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.NOMEM));
        return;
    };

    try memory.write(i32, fd_ptr, 0, opened_fd);

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

/// Read the contents of a symbolic link.
/// path_readlink(fd, path, path_len, buf, buf_len, bufused) -> errno
pub fn path_readlink(vm: *VirtualMachine) WasmError!void {
    const bufused_ptr = vm.popOperand(u32);
    const buf_len = vm.popOperand(u32);
    const buf_ptr = vm.popOperand(u32);
    const path_len = vm.popOperand(u32);
    const path_ptr = vm.popOperand(u32);
    const dir_fd = vm.popOperand(i32);

    const memory = try vm.inst.getMemory(0);
    const data = memory.memory();

    const sub_path = data[path_ptr..][0..path_len];
    const buf = data[buf_ptr..][0..buf_len];

    // Validate path for security: reject absolute paths and parent directory references
    if (sub_path.len > 0 and sub_path[0] == '/') {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.NOTCAPABLE));
        return;
    }
    if (mem.indexOf(u8, sub_path, "..") != null) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.NOTCAPABLE));
        return;
    }

    const host_fd = vm.getHostFd(dir_fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }

    const result = posix.readlinkat(host_fd, sub_path, buf) catch |err| {
        try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
        return;
    };

    try memory.write(u32, bufused_ptr, 0, @intCast(result.len));
    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

// FIXME: implement
pub fn poll_oneoff(vm: *VirtualMachine) WasmError!void {
    const param0 = vm.popOperand(i32);
    const param1 = vm.popOperand(i32);
    const param2 = vm.popOperand(i32);
    const param3 = vm.popOperand(i32);
    std.debug.print("Unimplemented: poll_oneoff({}, {}, {}, {})\n", .{ param0, param1, param2, param3 });
    try vm.pushOperand(u64, 0);
    @panic("Unimplemented: poll_oneoff");
}

pub fn proc_exit(vm: *VirtualMachine) WasmError!void {
    const param0 = vm.popOperand(i32);
    const code: u32 = @abs(param0);
    posix.exit(@truncate(code));
}

pub fn random_get(vm: *VirtualMachine) WasmError!void {
    const buf_len = vm.popOperand(u32);
    const buf_ptr = vm.popOperand(u32);

    const memory = try vm.inst.getMemory(0);
    const data = memory.memory();

    std.crypto.random.bytes(data[buf_ptr .. buf_ptr + buf_len]);

    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

// FIXME: figure out complete mapping of errors we expect from
//        zig std lib and how they map to the complete wasi.errno_t
//        struct. Such a complete mapping would be useful to add to
//        the zig std lib?
fn toWasiError(err: anyerror) wasi.errno_t {
    return switch (err) {
        error.AccessDenied => .ACCES,
        error.DiskQuota => .DQUOT,
        error.InputOutput => .IO,
        error.FileTooBig => .FBIG,
        error.NoSpaceLeft => .NOSPC,
        error.BrokenPipe => .PIPE,
        error.NotOpenForWriting => .BADF,
        error.NotOpenForReading => .BADF,
        error.SystemResources => .NOMEM,
        error.FileNotFound => .NOENT,
        error.PathAlreadyExists => .EXIST,
        error.IsDir => .ISDIR,
        error.Unseekable => .SPIPE, // ESPIPE: Illegal seek (e.g., on pipes/sockets)
        error.InvalidArgument => .INVAL,
        error.PermissionDenied => .PERM,
        else => std.debug.panic("WASI: Unhandled zig stdlib error: {s}", .{@errorName(err)}),
    };
}

// FIXME: can we write a program that exercises all of the zig file kinds and wasi filetypes?
//        Again, such a mapping would be useful to add to the zig std lib?
fn toWasiFileType(kind: fs.File.Kind) wasi.filetype_t {
    return switch (kind) {
        .block_device => .BLOCK_DEVICE,
        .character_device => .CHARACTER_DEVICE,
        .directory => .DIRECTORY,
        .sym_link => .SYMBOLIC_LINK,
        .file => .REGULAR_FILE,
        .unknown => .UNKNOWN,

        .named_pipe,
        .unix_domain_socket,
        .whiteout,
        .door,
        .event_port,
        => .UNKNOWN,
    };
}

fn toWasiTimestamp(ns: i128) u64 {
    return @as(u64, @intCast(ns));
}

const _ = std.testing.refAllDecls();

pub fn fd_tell(vm: *VirtualMachine) WasmError!void {
    const offset_ptr = vm.popOperand(u32);
    const fd = vm.popOperand(i32);

    const host_fd = vm.getHostFd(fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }
    const current_offset = posix.lseek_CUR_get(host_fd) catch |err| {
        try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
        return;
    };

    const memory = try vm.inst.getMemory(0);
    try memory.write(u64, offset_ptr, 0, current_offset);
    try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
}

fn toWasiFiletype(native_type: u8) wasi.filetype_t {
    return switch (native_type) {
        std.posix.DT.BLK => .BLOCK_DEVICE,
        std.posix.DT.CHR => .CHARACTER_DEVICE,
        std.posix.DT.DIR => .DIRECTORY,
        std.posix.DT.LNK => .SYMBOLIC_LINK,
        std.posix.DT.REG => .REGULAR_FILE,
        std.posix.DT.SOCK => .SOCKET_STREAM,
        else => .UNKNOWN,
    };
}

fn writeWasiDirent(
    mem_data: []u8,
    memory: anytype,
    offset: u32,
    next_cookie: u64,
    inode: u64,
    name: []const u8,
    filetype: wasi.filetype_t,
) !void {
    try memory.write(u64, offset, 0, next_cookie);
    try memory.write(u64, offset + 8, 0, inode);
    try memory.write(u32, offset + 16, 0, @as(u32, @intCast(name.len)));
    mem_data[offset + 20] = @intFromEnum(filetype);
    const name_offset = offset + @sizeOf(wasi.dirent_t);
    @memcpy(mem_data[name_offset..][0..name.len], name);
}

pub fn fd_readdir(vm: *VirtualMachine) WasmError!void {
    const bufused_ptr = vm.popOperand(u32);
    const cookie = vm.popOperand(u64);
    const buf_len = vm.popOperand(u32);
    const buf_ptr = vm.popOperand(u32);
    const fd = vm.popOperand(i32);

    const memory = try vm.inst.getMemory(0);
    const mem_data = memory.memory();
    const host_fd = vm.getHostFd(fd);
    if (host_fd == @as(posix.fd_t, @bitCast(@as(i32, -1)))) {
        try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.BADF));
        return;
    }
    const dirent_size: u32 = @sizeOf(wasi.dirent_t);

    switch (native_os) {
        .linux => {
            posix.lseek_SET(host_fd, 0) catch |err| {
                try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
                return;
            };

            var entry_idx: u64 = 0;
            var bytes_used: u32 = 0;
            var buffer_full = false;

            while (true) {
                var kernel_buf: [8192]u8 = undefined;
                const nread = std.os.linux.getdents64(host_fd, &kernel_buf, kernel_buf.len);
                if (nread == 0) break;
                if (@as(isize, @bitCast(nread)) < 0) break;

                var kernel_offset: usize = 0;
                while (kernel_offset < nread) {
                    const entry: *align(1) std.os.linux.dirent64 = @ptrCast(&kernel_buf[kernel_offset]);
                    const name = mem.sliceTo(@as([*:0]u8, @ptrCast(&entry.name)), 0);
                    kernel_offset += entry.reclen;

                    if (entry_idx < cookie) {
                        entry_idx += 1;
                        continue;
                    }

                    const entry_size: u32 = dirent_size + @as(u32, @intCast(name.len));
                    if (bytes_used + entry_size > buf_len) {
                        // Buffer is full but there are more entries
                        buffer_full = true;
                        break;
                    }

                    try writeWasiDirent(mem_data, memory, buf_ptr + bytes_used, entry_idx + 1, entry.ino, name, toWasiFiletype(entry.type));
                    bytes_used += entry_size;
                    entry_idx += 1;
                }

                if (buffer_full) break;
            }

            // WASI spec: bufused < buf_len signals EOF.
            // If buffer is full but there are more entries, return buf_len to signal "continue reading".
            // Zero-fill remaining buffer space to avoid garbage data.
            if (buffer_full and bytes_used < buf_len) {
                @memset(mem_data[buf_ptr + bytes_used .. buf_ptr + buf_len], 0);
                try memory.write(u32, bufused_ptr, 0, buf_len);
            } else {
                try memory.write(u32, bufused_ptr, 0, bytes_used);
            }
            try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
        },

        .macos, .ios, .tvos, .watchos, .visionos => {
            posix.lseek_SET(host_fd, 0) catch |err| {
                try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
                return;
            };

            var entry_idx: u64 = 0;
            var bytes_used: u32 = 0;
            var seek: i64 = 0;
            var buffer_full = false;

            while (true) {
                var kernel_buf: [8192]u8 align(@alignOf(std.c.dirent)) = undefined;
                const nread = std.c.getdirentries(host_fd, &kernel_buf, kernel_buf.len, &seek);
                if (nread <= 0) break;

                var kernel_offset: usize = 0;
                while (kernel_offset < @as(usize, @intCast(nread))) {
                    const entry: *align(1) std.c.dirent = @ptrCast(&kernel_buf[kernel_offset]);
                    const name = @as([*]const u8, @ptrCast(&entry.name))[0..entry.namlen];
                    kernel_offset += entry.reclen;

                    if (entry_idx < cookie) {
                        entry_idx += 1;
                        continue;
                    }

                    const entry_size: u32 = dirent_size + @as(u32, @intCast(name.len));
                    if (bytes_used + entry_size > buf_len) {
                        // Buffer is full but there are more entries
                        buffer_full = true;
                        break;
                    }

                    try writeWasiDirent(mem_data, memory, buf_ptr + bytes_used, entry_idx + 1, entry.ino, name, toWasiFiletype(entry.type));
                    bytes_used += entry_size;
                    entry_idx += 1;
                }

                if (buffer_full) break;
            }

            // WASI spec: bufused < buf_len signals EOF.
            // If buffer is full but there are more entries, return buf_len to signal "continue reading".
            // Zero-fill remaining buffer space to avoid garbage data.
            if (buffer_full and bytes_used < buf_len) {
                @memset(mem_data[buf_ptr + bytes_used .. buf_ptr + buf_len], 0);
                try memory.write(u32, bufused_ptr, 0, buf_len);
            } else {
                try memory.write(u32, bufused_ptr, 0, bytes_used);
            }
            try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
        },

        .freebsd, .openbsd, .netbsd, .dragonfly => {
            posix.lseek_SET(host_fd, 0) catch |err| {
                try vm.pushOperand(u64, @intFromEnum(toWasiError(err)));
                return;
            };

            var entry_idx: u64 = 0;
            var bytes_used: u32 = 0;
            var buffer_full = false;

            while (true) {
                var kernel_buf: [8192]u8 align(@alignOf(std.c.dirent)) = undefined;
                const nread = std.c.getdents(host_fd, &kernel_buf, kernel_buf.len);
                if (nread <= 0) break;

                var kernel_offset: usize = 0;
                while (kernel_offset < @as(usize, @intCast(nread))) {
                    const entry: *align(1) std.c.dirent = @ptrCast(&kernel_buf[kernel_offset]);
                    const name = @as([*]const u8, @ptrCast(&entry.name))[0..entry.namlen];
                    kernel_offset += entry.reclen;

                    if (entry_idx < cookie) {
                        entry_idx += 1;
                        continue;
                    }

                    const entry_size: u32 = dirent_size + @as(u32, @intCast(name.len));
                    if (bytes_used + entry_size > buf_len) {
                        // Buffer is full but there are more entries
                        buffer_full = true;
                        break;
                    }

                    try writeWasiDirent(mem_data, memory, buf_ptr + bytes_used, entry_idx + 1, entry.fileno, name, toWasiFiletype(entry.type));
                    bytes_used += entry_size;
                    entry_idx += 1;
                }

                if (buffer_full) break;
            }

            // WASI spec: bufused < buf_len signals EOF.
            // If buffer is full but there are more entries, return buf_len to signal "continue reading".
            // Zero-fill remaining buffer space to avoid garbage data.
            if (buffer_full and bytes_used < buf_len) {
                @memset(mem_data[buf_ptr + bytes_used .. buf_ptr + buf_len], 0);
                try memory.write(u32, bufused_ptr, 0, buf_len);
            } else {
                try memory.write(u32, bufused_ptr, 0, bytes_used);
            }
            try vm.pushOperand(u64, @intFromEnum(wasi.errno_t.SUCCESS));
        },

        else => {
            @compileError("fd_readdir not implemented for this platform");
        },
    }
}
